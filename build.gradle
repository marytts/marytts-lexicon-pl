plugins {
    id 'de.dfki.mary.lexicon-compiler' version '0.3.0'
    id 'maven-publish'
}

group 'de.dfki.mary'
version '0.1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

task processLexicon {
    def srcFile = file('modules/pl/lexicon/pl.txt')
    ext.destFile = file("$buildDir/pl.txt")
    inputs.files srcFile
    outputs.files destFile
    doLast {
        def mapping = [:]
        destFile.withWriter('UTF-8') { writer ->
            srcFile.eachLine('UTF-8') { line ->
                def (word, phonemeStr) = line.split('\t', 2)
                def phonemes = phonemeStr.split()
                def mappedPhonemes = phonemes.collect {
                    mapping[it] ?: it
                }.join('').replaceAll(~/\./, '-')
                writer.println "$word $mappedPhonemes"
            }
        }
    }
}

compileLexicon {
    dependsOn processLexicon
    lexiconFile = processLexicon.destFile
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url "$rootProject.buildDir/repo"
        }
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/marytts/marytts/marytts'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}
