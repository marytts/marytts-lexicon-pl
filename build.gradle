plugins {
    id 'de.dfki.mary.lexicon-compiler' version '0.3.0'
    id 'maven-publish'
    id 'signing'
}

group 'de.dfki.mary'
version '0.1.0-SNAPSHOT'
description 'Polish lexicon for MaryTTS'

ext {
    url = 'https://github.com/marytts/marytts-lexicon-pl'
    isReleaseVersion = !version.endsWith('SNAPSHOT')
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.register('processLexicon') {
    def srcFile = file('modules/pl/lexicon/pl.txt')
    ext.destFile = objects.fileProperty()
            .convention(layout.buildDirectory.file('pl.txt'))

    inputs.file srcFile
    outputs.file destFile

    doLast {
        destFile.get().asFile.withWriter('UTF-8') { writer ->
            srcFile.eachLine('UTF-8') { line ->
                def (word, phonemeStr) = line.split('\t', 2)
                def phonemes = phonemeStr
                        .replaceAll(~/\s/, '')
                        .replaceAll(~/\./, '-')
                writer.println "$word $phonemes"
            }
        }
    }
}

compileLexicon {
    lexiconFile.set processLexicon.destFile
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = project.url
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0 only'
                        url = 'https://spdx.org/licenses/LGPL-3.0-only.html'
                    }
                }
                developers {
                    developer {
                        name = 'Ingmar Steiner'
                        id = 'psibre'
                    }
                    developer {
                        name = 'Jolanta Bachan'
                        id = 'jolabachan'
                    }
                }
                contributors {
                    contributor {
                        name = 'Sascha Brawer'
                        url = 'https://github.com/brawer'
                    }
                }
                scm {
                    connection = "scm:git:${project.url}.git"
                    developerConnection = "scm:git:ssh://git@github.com:marytts/${project.name}.git"
                    url = project.url
                }
            }
        }
    }
    repositories {
        maven {
            name 'OSSRH'
            url isReleaseVersion ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    : 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials(PasswordCredentials)
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications.mavenJava
}

tasks.withType(Sign).configureEach {
    onlyIf { isReleaseVersion }
}
